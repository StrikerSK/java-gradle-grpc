plugins {
    id 'application'
    id 'com.google.protobuf' version '0.8.19'
    id 'io.spring.dependency-management' version '1.0.14.RELEASE'
    id 'idea'
    id 'java'
}

group 'io.grpc.todo'
version '0.0.1-SNAPSHOT'

repositories {
    maven { // The google mirror is less flaky than mavenCentral()
        url "https://maven-central.storage-download.googleapis.com/maven2/"
    }
    mavenCentral()
    mavenLocal()
}

// IMPORTANT: You probably want the non-SNAPSHOT version of gRPC. Make sure you
// are looking at a tagged version of the example and not "master"!

// Feel free to delete the comment at the next line. It is just for safely
// updating the version in our release process.
def grpcVersion = '1.49.1' // CURRENT_GRPC_VERSION
def protobufVersion = '3.21.6'
def protocVersion = protobufVersion

dependencies {
    //Protobuf dependencies
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"
    runtimeOnly "io.grpc:grpc-netty:${grpcVersion}"

    compileOnly "org.apache.tomcat:annotations-api:6.0.53"

    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
//    testImplementation "junit:junit:4.13.2"
//    testImplementation 'org.mockito:mockito-core:4.8.0'
}

dependencyManagement {
    imports {
        mavenBom "io.grpc:grpc-bom:${grpcVersion}"
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protocVersion}"
    }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        java {
            srcDirs 'todo-grpc-server/build/generated/source/proto/main/grpc'
            srcDirs 'todo-grpc-server/build/generated/source/proto/main/java'
        }
    }
}